buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'


ext.configFile = file("..//common//common-1.16.properties")
ext.config = parseConfig(configFile)

version = "${config.mc_version}-${mod_version}.${mod_subversion}"
group = "${mod_group}"
archivesBaseName = "${mod_archivename}"

repositories {
	maven {
		name = 'JEI Maven'
		url = 'http://dvs1.progwml6.com/files/maven'
	}
		maven {
		name = 'Immersive Maven'
		url = 'http://maven.blamejared.com'
	}
}

sourceSets {
	main {
		resources {
			srcDirs = [
				"$rootDir/src/main/resources",
				"$rootDir/src/generated/resources"
			]
		}
	}
}

minecraft {
    mappings channel: "${config.mapping_channel}", version: "${config.mapping_version}"

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'info'
			property 'fml.earlyprogresswindow', 'false'
			mods {
				immersivetechnology {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'info'
			property 'fml.earlyprogresswindow', 'false'
			mods {
				immersivetechnology {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'info'
			property 'fml.earlyprogresswindow', 'false'
			
			args '--mod', 'immersivetechnology', '--all', '--output', file('src/generated/resources/'), '--validate', '--existing', sourceSets.main.resources.srcDirs[0], '--existing', 'C:/Users/TwistedGate/.gradle/caches/forge_gradle/deobf_dependencies/blusunrize/immersiveengineering/ImmersiveEngineering/1.16.3-4.0.1-121_mapped_snapshot_20200912-1.16.2/ImmersiveEngineering-1.16.3-4.0.1-121_mapped_snapshot_20200912-1.16.2.jar'
			
			mods {
				immersivetechnology {
					source sourceSets.main
				}
			}
		}
	}
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.mc_version}-${config.forge_version}"
	compile fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:${config.mc_version}-${config.imme_version}")
	compile fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${config.mc_version}:${config.crft_version}")
    compileOnly fg.deobf("mezz.jei:jei-${config.mc_version}:${config.jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${config.mc_version}:${config.jei_version}")
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": "${mod_id}",
            "Specification-Vendor": "${mod_author}",
            "Specification-Version": "${mod_subversion}",
            "Implementation-Title": "${mod_id}",
            "Implementation-Version": "${mod_version}",
            "Implementation-Vendor" :"${mod_author}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'META-INF/mods.toml'
		expand 'version': project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'META-INF/mods.toml'
	}
	outputs.upToDateWhen { false }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) { type 'jar'; builtBy 'reobfJar'; }